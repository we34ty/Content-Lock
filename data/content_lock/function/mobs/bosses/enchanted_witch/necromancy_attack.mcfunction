execute if entity @s[scores={content_lock.attack_timer=0}] run scoreboard players add @s content_lock.boss_exhaustion 135
execute if entity @s[scores={content_lock.attack_timer=0}] run scoreboard players add @s content_lock.attack_timer 120
execute if entity @s[scores={content_lock.attack_timer=120}] at @e[type=#boss_can_damage,tag=content_lock.im_a_target] run summon area_effect_cloud ~ ~ ~ {Duration:41,Tags:[content_lock.enchanted_witch.necromancy_attack.area]}
execute if entity @s[scores={content_lock.attack_timer=80..120}] at @e[tag=content_lock.enchanted_witch.necromancy_attack.area,distance=0..40] run particle soul ~ ~ ~ 1 0.3 1 0.1 20 force @a
execute if entity @s[scores={content_lock.attack_timer=80}] run scoreboard players set L content_lock.temp1 0
execute if entity @s[scores={content_lock.attack_timer=80}] as @e[tag=content_lock.enchanted_undead] run scoreboard players add L content_lock.temp1 1
execute if entity @s[scores={content_lock.attack_timer=80}] if score L content_lock.temp1 matches 2.. as @e[tag=content_lock.enchanted_undead,limit=1,sort=random] run function content_lock:mobs/bosses/boss_despawn
execute if entity @s[scores={content_lock.attack_timer=80}] at @e[tag=content_lock.enchanted_witch.necromancy_attack.area,distance=0..40] run summon zombie ~ ~ ~ {DeathLootTable:"content_lock:entities/enchanted_undead",CanBreakDoors:1b,CustomName:'[{"translate":"content_lock:mob.enchanted_undead","color":"light_purple","bold":true}]',CustomNameVisible:1b,Silent:1b,Tags:["content_lock.can_cast_fireball","content_lock.progression_not_scaled","content_lock.enchanted_undead"],HandItems:[{id:enchanted_book,count:1},{}],HandDropChances:[0f,0f],ArmorItems:[{id:diamond_boots,components:{trim:{pattern:bolt,material:resin}},count:1},{id:diamond_leggings,components:{trim:{pattern:bolt,material:resin}},count:1},{id:diamond_chestplate,components:{trim:{pattern:bolt,material:resin}},count:1},{id:skeleton_skull,count:1}],ArmorDropChances:[0f,0f,0f,0f],attributes:[{id:attack_damage,base:5f},{id:max_health,base:8f}]}
execute if entity @s[scores={content_lock.attack_timer=80}] at @e[tag=content_lock.enchanted_witch.necromancy_attack.area,distance=0..40] run playsound entity.zombie.death hostile @a ~ ~ ~ 2 0.5
execute if entity @s[scores={content_lock.attack_timer=80}] at @e[tag=content_lock.enchanted_witch.necromancy_attack.area,distance=0..40] run particle soul ~ ~ ~ 0 0 0 1 100
execute if entity @s[scores={content_lock.attack_timer=80}] run tag @s remove content_lock.enchanted_witch.necromancy_attack